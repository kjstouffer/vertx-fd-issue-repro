group 'com.bluemedora'

buildscript {
    // Build script variables
    ext {
        kotlinVersion = '1.2.10'
        jvmVersion = '1.8'
    }
    // Setup plugin dependencies for the Gradle build itself
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    // Dependencies of the build script itself
    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlinVersion
    }
}

// Apply these plugins so they take effect
['java', 'kotlin', 'idea', 'application'].each { plugin ->
    apply plugin: plugin
}

// Set the main class to be able to run the app with the 'run' task
mainClassName = "MainKt"

// Override default target of JVM 1.6 for both production and test variants
['', 'Test'].each { variant ->
    this."compile${variant}Kotlin".kotlinOptions.jvmTarget = jvmVersion
}

// Set up repositories for library dependencies
repositories {
    // Look for Vertxuno in our Artifactory
    maven {
        url 'https://bluemedora.jfrog.io/bluemedora/libs-release-local'
        credentials {
            username = artifactory_user
            password = artifactory_password
        }
    }
    // Look for the rest in JCenter
    jcenter()
}

// Dependencies of the project
dependencies {
    compile 'io.vertx:vertx-web-client:3.5.4'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
}

sourceSets {
    main.kotlin.srcDirs = ['src/main/kotlin']
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}